#
#5000
#500
#100
#2700
#C:\Users\Andrew\Documents\CS 455\TestCases\Classes\Simulator\AHPROGS\AHPROG6.PRT
#
*ANDREW HAMILTON
*PROGRAMMING ASSIGNMENT #5
*4/17/13
*THIS PROGRAM WILL MAINTAIN A BALANCE FOR A SAVINGS ACCOUNT.
*IT WILL ACCEPT AS INPUT LINES OF DATA WITH THE FIRST CHARACTER
*OF EACH LINE BEING A CODE (0-3) TO SIGNAL A DEPOSIT, WITHDRAWAL,
*OR INTEREST CALCULATION, FOLLOWED BY THE AMOUNT. THE PROGRAM
*WILL CREATE A REPORT BASED ON THE INPUT.
UPDATE5  START
         STM   14,12,12(13)
         BALR  12,0
         USING HERE,12
HERE     ST    13,SAVEAREA+4
         LA    13,SAVEAREA
*
*
*    PRINT THE TOP OF THE REPORT STATEMENT
*
         XPRNT =C' TRANS.    SAVINGS     ACCOUNT    CURRENT',42
         XPRNT =C' CODE      AMOUNT      RATE       BALANCE',42
         BAL   11,CLROUT
* INITIALIZE COUNTERS TO ZERO AND A PACKED LOCATION
* TO ONE TO BE USED TO INCREMENT
*        
         ZAP   TOTDEP(8),=P'0'
         ZAP   NUMDEP(3),=P'0'
         ZAP   TOTWTH(8),=P'0'
         ZAP   NUMWTH(3),=P'0'
         ZAP   ONE(1),=P'1'
         SR    2,2
         LA    3,1
*
* READ UNTIL END OF FILE LOOP
TOPREAD  XREAD LINE,10
         BC    B'0100',ENDREAD
*
* A SERIES OF BRANCH ON CONDITIONS ARE USED TO DETERMINE
* WHAT THE FIRST CHARACTER IS. A CALL IS MADE TO AN 
* INTERNAL SUBROUTINE BASED ON THE FIRST CHARACTER.
*
         CLI   LINE,C'0'
         BC    B'0111',DEP
*
* CHECK TO SEE IF AN ACCOUNT HAS ALREADY BEEN MADE
* IF THERE HAS ALREADY BEEN AN INITIAL ACCOUNT,
* PRINT OUT THE OLD ACCOUNT AND START A NEW ONE
*
         CR    2,3
         BC    B'0100',INIT  
         BAL   11,PRNTCRRN   
         BAL   11,CLROUT
         XPRNT =C' ',1
         BAL   11,PRNTDEP
         BAL   11,CLROUT
         XPRNT =C' ',1
         BAL   11,PRNTWITH
         BAL   11,CLROUT
         BAL   11,CLRALL
         XPRNT =C' TRANS.    SAVINGS     ACCOUNT    CURRENT',42
         XPRNT =C' CODE      AMOUNT      RATE    BALANCE',42
         BAL   11,INITIAL
         BC    B'1111',BOTTOM
         BAL   11,CLRALL
INIT     BAL   11,INITIAL
         BC    B'1111',BOTTOM
* 
DEP      CLI   LINE,C'1'
         BC    B'0111',WITHDRW
         BAL   11,DEPOSIT
         BC    B'1111',BOTTOM
*
WITHDRW  CLI   LINE,C'2'
         BC    B'0111',INTR
         BAL   11,WITHDRAW
         BC    B'1111',BOTTOM 
*
INTR     BAL   11,INTEREST
BOTTOM   MVC   TEMP(13),=X'40206B2020206B2021204B2020'
         BAL   11,PRNTCRRN
         BAL   11,CLROUT
         BC    B'1111',TOPREAD
*
ENDREAD  BAL   11,CLROUT
         XPRNT =C' ',1 
         XPRNT =C' CURRENT BALANCE: ',18
         BAL   11,PRNTCRRN
         XPRNT =C' ',1
         BAL   11,PRNTDEP
         BAL   11,CLROUT
         XPRNT =C' ',1
         BAL   11,PRNTWITH
         L     13,SAVEAREA+4
         LM    14,12,12(13)
         BR    14
SAVEAREA DS    18F
LINE     DS    ZL10
CC       DS    C'0'
OUT      DS    CL80
TEMP     DS    CL17
TEMP2    DS    CL6
CURRENT  DS    PL5
LOWEST   DS    PL5
DEPOS    DS    PL5
WITH     DS    PL5
INTER    DS    PL2
TOTDEP   DS    PL8
TOTWTH   DS    PL8
NUMDEP   DS    PL3
NUMWTH   DS    PL3
ONE      DS    PL1
*
*
INITIAL  PACK  CURRENT(5),LINE+1(9)
         ZAP   LOWEST(5),CURRENT(5) INITIALIZE LOWEST VAL 
         MVI   OUT+1,C'0'  MOVE IN A ZERO FOR OUTPUT
*
         MVC   TEMP(13),=X'40206B2020206B2021204B2020'
         EDMK  TEMP(13),CURRENT
         BCTR  1,0
         MVI   0(1),C'$' SEND THE INITIAL VALUE AND DOLLAR
         MVC   OUT+6(13),TEMP SIGN TO THE OUTPUT LINE
         LA    2,1(2)
         BR    11
*
*
DEPOSIT  PACK  DEPOS(5),LINE+1(9)
         AP    CURRENT(5),DEPOS(5) ADD DEPOSIT TO CURRENT AMT AND 
         MVI   OUT+1,C'1'  SEND EACH VALUE TO THE OUT LIN
*
         MVC   TEMP(13),=X'40206B2020206B2021204B2020'
         EDMK  TEMP(13),DEPOS
         BCTR  1,0
         MVI   0(1),C'$'
         MVC   OUT+6(13),TEMP
         AP    TOTDEP(8),DEPOS(5)  ACCUMULATE TOTAL DEPOSITS
         AP    NUMDEP(3),ONE(1) AND TOTAL AMOUNT
         BR    11
*
*
WITHDRAW PACK  WITH(5),LINE+1(9)
         SP    CURRENT(5),WITH(5)
         MVI   OUT+1,C'2'  WORKS THE SAME AS DEPOSIT BUT 
*        SUBTRACTS FROM TOTAL AMOUNT
         MVC   TEMP(13),=X'40206B2020206B2021204B2020'
         EDMK  TEMP(13),WITH
         BCTR  1,0
         MVI   0(1),C'$'
         MVC   OUT+6(13),TEMP
         AP    TOTWTH(8),WITH(5) ACCUMULATE NUM WITHDRAWALS
         AP    NUMWTH(3),ONE(1) AND TOTAL AMOUNT
         CP    CURRENT(5),LOWEST(5)
         BC    B'1011',WITHBOT  CHECK FOR NEW LOWEST VAL
         ZAP   LOWEST(5),CURRENT(5) TO BE USED WITH INTEREST 
WITHBOT  BR    11  CALCULATIONS
*
*
INTEREST MVI   OUT+1,C'3'
         PACK  INTER(2),LINE+1(3)
         MVC   TEMP(5),=X'40214B2020'
         ED    TEMP(5),INTER
         MVC   OUT+23(5),TEMP
         BR    11
*
*
CLROUT   MVI   OUT,C' '
         MVC   OUT+1(79),OUT
         BR    11
*
*
*PRINTS THE CURRENT AMOUNT IN THE BANK ACCOUNT
PRNTCRRN MVC   TEMP(13),=X'40206B2020206B2021204B2020'
         EDMK  TEMP(13),CURRENT
         BCTR  1,0    
         MVI   0(1),C'$'
         MVC   OUT+32(13),TEMP
         XPRNT CC,46
         BR    11
*
*
*PRINTS NUMBER OF DEPOSITS AND TOTAL AMOUNT
PRNTDEP  MVC   TEMP2(6),=X'402020202020'
         ED    TEMP2(6),NUMDEP
         MVC   OUT+13(6),TEMP2
         MVC   OUT(13),=C'# DEPOSITS = '
         XPRNT CC,23
         XPRNT =C' ',1
         MVC   TEMP(16),=X'40202020202020202020202020202020'
         ED    TEMP(16),TOTDEP
         MVC   OUT(24),=C'TOTAL AMOUNT OF DEPOSITS'
         MVC   OUT+26(16),TEMP
         XPRNT CC,47
         BR    11
*
*
*PRINTS NUMBER OF WITHDRAWAL AND TOTAL AMOUNT
PRNTWITH MVC   OUT(16),=C'# WITHDRAWALS = '
         MVC   TEMP2(6),=X'402020202020'
         ED    TEMP2(6),NUMWTH
         MVC   OUT+17(6),TEMP2
         XPRNT CC,32
         XPRNT =C' ',1
         MVC   OUT(24),=C'AMOUNT OF WITHDRAWALS = '
         MVC   TEMP(16),=X'40202020202020202020202020202020'
         ED    TEMP(16),TOTWTH
         MVC   OUT+18(16),TEMP
         XPRNT CC,50
         XPRNT =C' ',1
         BR    11
CLRALL   ZAP   TOTDEP(8),=P'0'
         ZAP   NUMDEP(3),=P'0'
         ZAP   TOTWTH(8),=P'0'
         ZAP   NUMWTH(3),=P'0'
         BR    11
         END   UPDATE5
$ENTRY
00003213111
10004552222
20001123111
00002345678
10000123345
20000085632
00000223546
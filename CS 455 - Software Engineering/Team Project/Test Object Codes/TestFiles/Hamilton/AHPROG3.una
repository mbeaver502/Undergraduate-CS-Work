#
#5000
#500
#100
#2700
#
#
*ANDREW HAMILTON
*PROGRAMMING ASSIGNMENT #3
*3/21/13
*THIS PROGRAM WILL READ IN UP TO 30 LINES OF DATA CONTAINING ONE
*INTEGER BETWEEN 0 AND 100 PER LINE. IT WILL THEN SORT THE NUMBERS
*IN DESCENDING ORDER AND PRINT THEM OUT.
*REGISTER KEY-
*REG 2 - USED AS A POINTER TO THE TOP OF THE TABLE AS WELL AS THE 
*        CONTROL REGISTER FOR THE BXLE LOOPS
*REG 3 - USED WITH XDECI AND XDECO TO INPUT THE NUMBERS INTO THE 
* TABLE IN THE INPUT ROUTINE AND OUTPUT THEM IN THE PRINT
* ROUTINE
*REG 4 - USED AS THE STEP SIZE REGISTER IN THE BXLE LOOPS
*REG 5 - USED AS THE TERMINAL VALUE IN THE BXLE LOOP, POINTS TO
* THE END OF THE TABLE.
*REG 6 - USED TO TREVERSE THE TABLE IN THE INNER LOOP OF THE 
* SORT ROUTINE
*REG 7 - POINTS TO THE LARGEST VALUE FOUND ON EACH LOOP OF THE 
* SORT ROUTINE
*REG 8 - USED TO COMPARE NUMBERS IN THE TABLE IN THE SORT ROUTINE
*REG 9,10 - USED AS TEMP REGISTERS WHEN SWAPPING VALUES
*REG 11 - USED AS THE BAL REGISTER TO CALL THE SUBROUTINES
AHPROG3  START
         STM   14,12,12(13)
         BALR  12,0
         USING HERE,12
HERE     ST    13,SAVEAREA+4
         LA    13,SAVEAREA
         SPACE
         BAL   11,INPUT    CALLS TO THE INTERNAL SUBROUTINES
         BAL   11,SORT
         BAL   11,PRINT
         SPACE
         L     13,SAVEAREA+4
         LM    14,12,12(13)
         BR    14
         SPACE
SAVEAREA DS    18F
LINE     DS    CL80
TABLE    DS    30F
CC       DC    C'0'
OUT      DS    CL12
         SPACE
         SPACE
*THE INPUT ROUTINE READS IN UP TO 30 LINES OF DATA AND INSERTS
*THEM INTO A TABLE. IT WILL ONLY ACCEPT NUMBERS BETWEEN 0 AND 100
INPUT    LA    2,TABLE     
         LA    4,4             SET UP REGISTERS FOR THE BXLE LOOP 
         LA    5,TABLE+120     
         LA    6,0             REG 6 AND 7 USED TO CHECK FOR VALID
         LA    7,100           INPUT
INPTLOOP XREAD LINE,80
         BC    B'0100',ENDREAD
         XDECI 3,LINE
         CR    3,6             COMPARE EACH INPUT VALUE WITH 0 AND
         BC    B'1011',NEXT    30, IF < 0 OR > 30, REG 2 IS 
         SR    2,4             DECREMENTED AND THE VALUE IS NOT INPUT
         BC    B'1111',BOTTOM
NEXT     CR    3,7
         BC    B'1101',NEXT2
         SR    2,4
         BC    B'1111',BOTTOM
NEXT2    ST    3,0(2)
BOTTOM   BXLE  2,4,INPTLOOP
ENDREAD  LR    5,2             SET REG 5 TO POINT TO THE BOTTOM OF
         LA    4,4             TABLE TO BE USED IN THE OTHER 
         SR    5,4             SUBROUTINES
         BR    11
         SPACE
         SPACE
*THE PRINT ROUTINE PRINTS THE NUBMERS IN THE TABLE FROM THE 
*TOP TO THE BOTTOM WHERE REG 5 IS POINTING
PRINT    LA    4,4
         LA    2,TABLE
PRNT     L     3,0(2)
         XDECO 3,OUT
         XPRNT CC,13
         BXLE  2,4,PRNT
         BR    11
         SPACE
         SPACE
*THE SORT ROUTINE USES THE SELECTION SORT ALGORITHM TO SORT THE
*TABLE IN DESCENDING ORDER
SORT     LA    2,TABLE         THE OUTER BXLE LOOP USES 2 AS THE 
         LA    4,4             CONDITION REG AND 5 AS THE TERMINAL 
OUTER    LA    6,0(2)          VALUE
         LA    7,0(2)          REG 7 HOLDS THE LARGEST VALUE FOUND
INNER    L     8,0(6)          
         C     8,0(7)          COMPARE 7 WITH ALL THE VALUES IN THE 
         BC    B'1101',ENDINNER   TABLE AND IF THEY ARE > REG 7,
         LA    7,0(6)          SET 7 TO POINT TO THE LARGER VALUE
ENDINNER BXLE  6,4,INNER
         L     9,0(2)          USE REG 9 AND 10 TO SWAP THE LARGEST
         L     10,0(7)         VALUE FOUND WITH THE TOP OF THE LIST-
         ST    9,0(7)          SWAP REG 2 AND REG 7
         ST    10,0(2)
         BXLE  2,4,OUTER
         BR    11              MAY NEED TO BE BR14
         END   AHPROG3
$ENTRY
5
17
22
1
7
65
44
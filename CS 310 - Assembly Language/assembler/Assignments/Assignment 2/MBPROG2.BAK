	TITLE	'PROGRAM PROLOGUE'
************************************************************************
*			                                     *
*  MICHAEL BEAVER	                                     *
*  CS 310, SPRING 2013	                                     *
*  PROGRAM 2                                                           *
*  DUE: MARCH 11, 2013                                                 *
*			                                     *
*  DESCRIPTION:                                                        *
*    THIS PROGRAM EXPECTS AS INPUT A DATA FILE CONTAINING 20 LINES OF  *
*    INTEGER DATA (ONE INTEGER PER LINE). THE PROGRAM WILL READ IN THE *
*    DATA FROM THE FILE AND STORE IT INTO A TABLE. NEXT, THE PROGRAM   *
*    WILL PRINT THE DATA FROM THE TABLE FROM FIRST-TO-LAST (IN THE     *
*    ORDER THE DATA WAS ORIGINALLY READ). NEXT, THE PROGRAM WILL PRINT *
*    THE DATA FROM THE TABLE FROM LAST-TO-FIRST (IN THE OPPOSITE ORDER *
*    THE DATA WAS ORIGINALLY READ). FINALLY, THE PROGRAM TERMINATES.   *
*			                                     *
*  REGISTER KEY:	                                     *
*    REG 2: INDEX POINTER FOR SUBROUTINES' LOOP-BASED TABLE OPERATIONS *
*    REG 3: DATA VALUE HOLDER FOR SUBROUTINES' OPERATIONS              *
*    REG 8: STEP-SIZE HOLDER FOR SUBROUTINES' LOOPS                    *
*    REG 9: TERMINAL VALUE HOLDER FOR SUBROUTINES' LOOPS               *
*    REG11: SUBROUTINE BRANCHING (BREAD CRUMBS)                        *
*			                                     *
************************************************************************
	TITLE 'MAIN PROGRAM'
MBPROG2  START
	SPACE 2
	STM	14,12,12(13)	   	BEGINNING HOUSEKEEPING
	BALR	12,0
	USING	SAVING,12
SAVING	ST	13,SAVEAREA+4
	LA	13,SAVEAREA
	SPACE 2
	BAL	11,DATAREAD		READ IN DATA TO TABLE	
	SPACE 2
	BAL	11,PRINTFTL		PRINT TABLE FIRST-TO-LAST
	SPACE 2
	BAL	11,PRINTLTF		PRINT TABLE LAST-TO-FIRST
	SPACE 2
	L	13,SAVEAREA+4	   	ENDING HOUSEKEEPING
	LM	14,12,12(13)
	BR	14
	SPACE 2 
SAVEAREA	DS	18F
DATATAB  DS    20F
CARD	DS	CL80
CARRIAGE	DC	C' '
OUTLINE	DS	CL12
	TITLE 'DATAREAD SUBROUTINE: READ DATA INTO TABLE'
DATAREAD	LA	2,DATATAB               TOP OF TABLE
	LA	8,4
	LA	9,DATATAB+76            BOTTOM OF TABLE
	SPACE 2 
TOPREAD	XREAD	CARD,80		LOOP THRU FILE & STORE DATA
	XDECI	3,CARD
	ST	3,0(2)
	BXLE	2,8,TOPREAD
	SPACE 2
	BR	11
	TITLE	'PRINTFTL SUBROUTINE: PRINT TABLE FIRST-TO-LAST'
PRINTFTL XPRNT	PFTLSTRT,41
	SPACE 2
	LA	2,DATATAB               TOP OF TABLE 
	LA	8,4
	LA	9,DATATAB+76            BOTTOM OF TABLE
	SPACE 2
TOPPFTL	L	3,0(2)		LOOP & PRINT FIRST-TO-LAST
	XDECO	3,OUTLINE
	XPRNT	CARRIAGE,13
	BXLE	2,8,TOPPFTL
	SPACE 2
	XPRNT	PFTLEND,29
	BR	11
PFTLSTRT	DC	C'1PRINTING TABLE FROM FIRST-TO-LAST VALUE:'
PFTLEND	DC	C' DONE PRINTING FIRST-TO-LAST.'
	TITLE	'PRINTLTF SUBROUTINE: PRINT TABLE LAST-TO-FIRST'
PRINTLTF	XPRNT PLTFSTRT,41
	SPACE	2
	LA	2,DATATAB+76            BOTTOM OF TABLE 
	L	8,=F'-4'
	LA	9,DATATAB-4             TOP OF TABLE-1
	SPACE 2
TOPPLTF	L	3,0(2)		LOOP & PRINT LAST-TO-FIRST
	XDECO	3,OUTLINE
	XPRNT	CARRIAGE,13
	BXH	2,8,TOPPLTF
	SPACE 2
	XPRNT	PLTFEND,29
	BR	11	
PLTFSTRT	DC	C'1PRINTING TABLE FROM LAST-TO-FIRST VALUE:'
PLTFEND	DC	C' DONE PRINTING LAST-TO-FIRST.'
	END	MBPROG2
$ENTRY DATA2.DAT
         TITLE 'PROGRAM PROLOGUE'
**********************************************************************
*  MICHAEL BEAVER                                                    *
*  CS 310, SPRING 2013                                               *
*  PROGRAM 6                                                         *
*  DUE: APRIL 22,2013                                                *
*                                                                    *
*  DESCRIPTION:                                                      *
*    THIS PROGRAM EXPECTS AS INPUT A DATA FILE CONTAINING 20 LINES OF*
*    INTEGER DATA (ONE INTEGER PER LINE). THE PROGRAM WILL READ IN   *
*    THE DATA FROM THE FILE AND STORE IT INTO A TABLE. NEXT, THE     *
*    PROGRAM WILL PRINT THE DATA FROM THE TABLE FROM FIRST-TO-LAST   *
*    (IN THE ORDER THE DATA WAS ORIGINALLY READ). FINALLY, THE       *
*    PROGRAM WILL PRINT THE DATA FROM THE TABLE FROM LAST-TO-FIRST   *
*    (IN THE OPPOSITE ORDER THE DATA WAS ORIGINALLY READ). THIS      *
*    VERSION OF THE PROGRAM USES EXTERNAL SUBROUTINES.               *
*                                                                    *
*  SUBROUTINES:                                                      *
*    DATAREAD: READS DATA FROM FILE AND STORES INTO DATA TABLE       *
*    PRINTFTL: PRINTS DATA FROM TABLE IN ORDER ORIGINALLY READ       *
*    PRINTLTF: PRINTS DATA FROM TABLE IN 'REVERSED' ORDER            *
**********************************************************************
         TITLE 'MAIN PROGRAM'
**********************************************************************
*  REGISTER KEY:                                                     *
*     REG1: USED TO PASS PARAMETER LIST TO SUBROUTINES               *
*    REG14: USED TO BRANCH TO SUBROUTINES                            *
*    REG15: PROVIDES ADDRESS OF SUBROUTINES TO BE BRANCHED TO        *
**********************************************************************
MBPROG6  START
         STM   14,12,12(13)            BEGINNING HOUSEKEEPING
	BALR	12,0
	USING	MAINHERE,12
MAINHERE	ST	13,MAINSAVE+4
	LA	13,MAINSAVE
	SPACE	2
	LA	1,PARMLIST
	L	15,=V(DATAREAD)         READ DATA FROM FILE INTO TABLE
	BALR	14,15
	SPACE
	LA	1,PARMLIST
	L	15,=V(PRINTFTL)         PRINT TABLE FIRST-TO-LAST
	BALR	14,15
	SPACE
	LA	1,PARMLIST
	L	15,=V(PRINTLTF)         PRINT TABLE LAST-TO-FIRST
	BALR	14,15
	SPACE 2
	L	13,MAINSAVE+4           ENDING HOUSEKEEPING
	LM	14,12,12(13)
	BR	14
MAINSAVE	DS	18F
DATATAB	DS	20F
CARRIAGE DC	C' '
OUTLINE  DS    CL12
PARMLIST DC	A(DATATAB)
         DC    A(CARRIAGE)
	LTORG
	TITLE	'DATAREAD: READ DATA INTO TABLE'
**********************************************************************
*  REGISTER KEY:                                                     *
*     REG1: USED TO OBTAIN PARAMETERS FROM MAIN                      *
*     REG2: HOLDS LOOP INDEX CORRESPONDING TO TABLE SPOTS            *
*     REG3: HOLDS DATA READ FROM FILE                                *
*     REG8: HOLDS LOOP STEP SIZE (4)                                 * 
*     REG9: HOLDS TERMINAL VALUE FOR LOOP (LAST SPOT IN TABLE)       * 
*    REG14: USED TO BRANCH BACK TO CALLER                            *
**********************************************************************
DATAREAD	CSECT
	STM	14,12,12(13)            BEGINNING HOUSEKEEPING
	BALR	12,0
	USING READHERE,12
READHERE	ST	13,READSAVE+4
	LA	13,READSAVE
	SPACE 2
	L	2,0(1)                  TOP OF THE TABLE
	LA	8,4		
	LA	9,76(2)		BOTTOM OF THE TABLE
	SPACE
READTOP  XREAD CARD,80                 LOOP THRU FILE & STORE DATA
	XDECI 3,CARD
	ST	3,0(2)
         BXLE  2,8,READTOP
	SPACE 2
	L	13,READSAVE+4           ENDING HOUSEKEEPING
	LM	14,12,12(13)
	BR	14
READSAVE DS	18F
CARD	DS	CL80
	LTORG
	TITLE	'PRINTFTL: PRINT TABLE FIRST-TO-LAST (AS READ)'
**********************************************************************
*  REGISTER KEY:                                                     *
*     REG1: USED TO GRAB PARAMETERS FROM MAIN                        *
*     REG2: LOOP INDEX CORRESPONDING TO SPOTS IN DATA TABLE          *
*     REG3: HOLDS VALUES GRABBED FROM TABLE                          *
*     REG8: HOLDS LOOP STEP SIZE (4)                                 *
*     REG9: HOLDS TERMINAL VALUE FOR LOOP (LAST SPOT IN TABLE)       *
*    REG10: HOLDS POINTER TO OUTPUT CARRIAGE CONTROL AND LINE        *
*    REG14: USED TO BRANCH BACK TO CALLER                            *
**********************************************************************
PRINTFTL CSECT
	STM	14,12,12(13)            BEGINNING HOUSEKEEPING
	BALR	12,0
	USING	FTLHERE,12
FTLHERE  ST	13,FTLSAVE+4
         LA	13,FTLSAVE
	SPACE 2
	XPRNT	PFTLSTRT,41
	SPACE
	L	2,0(1)                  TOP OF THE TABLE
         LA    8,4
         LA    9,76(2)                 BOTTOM OF THE TABLE
         LA	10,4(1)                 POINTER TO CARRIAGE
	SPACE
PFTLTOP  L     3,0(2)                  GRAB EACH VALUE AND PRINT IT
         XDECO 3,1(10)
         XPRNT 0(10),13
         BXLE  2,8,PFTLTOP
         SPACE
         XPRNT PFTLEND,29
	SPACE 2
	L	13,FTLSAVE+4            ENDING HOUSEKEEPING
	LM	14,12,12(13)
	BR	14
FTLSAVE  DS	18F
PFTLSTRT DC    C'1PRINTING TABLE FROM FIRST-TO-LAST VALUE:'
PFTLEND  DC    C' DONE PRINTING FIRST-TO-LAST.'
	LTORG
	TITLE 'PRINTLTF: PRINT TABLE LAST-TO-FIRST (REVERSE ORDER)'
**********************************************************************
*  REGISTER KEY:                                                     *
*     REG1: USED TO GRAB PARAMETERS FROM MAIN                        *
*     REG2: LOOP INDEX CORRESPONDING TO SPOTS IN DATA TABLE          *
*     REG3: HOLDS VALUE GRABBED FROM DATA TABLE                      *
*     REG8: HOLDS LOOP STEP SIZE (-4)                                *
*     REG9: HOLDS TERMINAL VALUE FOR LOOP (ONE SPOT ABOVE TABLE TOP) *
*    REG10: HOLDS POINTER TO CARRIAGE CONTROL AND OUTPUT LINE        *
*    REG14: USED TO BRANCH BACK TO CALLER                            *
**********************************************************************
PRINTLTF CSECT
	STM	14,12,12(13)            BEGINNING HOUSEKEEPING
         BALR  12,0
	USING LTFHERE,12
LTFHERE  ST	13,LTFSAVE+4
	LA	13,LTFSAVE
	SPACE 2
	XPRNT	PLTFSTRT,41
	SPACE
	L	2,0(1)		POINTER TO TOP OF THE TABLE
	LA	9,0(2)
	A	9,=F'-4'                POINTER TO TOP OF THE TABLE-4
	LA	2,76(2)                 POINTER TO BOTTOM OF THE TABLE
	L	8,=F'-4'
	LA	10,4(1)                 POINTER TO CARRIAGE	
	SPACE
PLTFTOP  L	3,0(2)                  LOOP & PRINT LAST-TO-FIRST
	XDECO	3,1(10)
	XPRNT	0(10),13
	BXH	2,8,PLTFTOP
	SPACE
	XPRNT	PLTFEND,29
	SPACE 2
	L	13,LTFSAVE+4            ENDING HOUSEKEEPING
	LM	14,12,12(13)
	BR	14
LTFSAVE  DS	18F
PLTFSTRT DC    C'1PRINTING TABLE FROM LAST-TO-FIRST VALUE:'
PLTFEND  DC    C' DONE PRINTING LAST-TO-FIRST.'
	LTORG
	END	MBPROG6
$ENTRY DATA2.TXT